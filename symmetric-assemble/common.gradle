import java.text.SimpleDateFormat;
import java.util.Date;

import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

configurations {
    sshAntTask
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.ow2.asm') {
            details.useVersion '5.0.3'
        }
    }
}

dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.10.5', 'com.jcraft:jsch:0.1.55'
}

allprojects {

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'org.jumpmind.symmetric'
    
    repositories {
        jcenter()
        maven { url "http://maven.jumpmind.com/repo" }
        mavenCentral()
        maven { url "http://maven.vaadin.com/vaadin-addons" }
        maven { url 'https://plugins.gradle.org/m2' }
        maven { url "http://packages.confluent.io/maven/" }
        maven { url "https://repository.mulesoft.org/nexus/content/repositories/public/" }
        maven { url "https://repo1.maven.org/maven2/" }
    }
        
    ext {
        majorMinorVersion = version.substring(0, version.indexOf(".", version.indexOf(".")+ 1))
    }
    
}

def ignoreDerivedResources(projectDescription, directories = ["build", "test-output"]) {
  def count = directories.count { file(it).exists() }
  if (count > 0) {
    def filter = projectDescription
      .appendNode("filteredResources")
      .appendNode("filter")
    filter.appendNode("id", System.currentTimeMillis().toString().trim())
    filter.appendNode("type", "26")
    filter.appendNode("name")
    def matcher = filter.appendNode("matcher")
    matcher.appendNode("id", "org.eclipse.ui.ide.orFilterMatcher")
    def arguments = matcher.appendNode("arguments")
    directories.each {
      if (file(it).exists()) {
        def dirMatcher = arguments.appendNode("matcher")
        dirMatcher.appendNode("id", "org.eclipse.ui.ide.multiFilter")
        dirMatcher.appendNode("arguments", "1.0-projectRelativePath-matches-false-false-${it}")
      }
    }
  }
}  

subprojects { subproject ->

    apply plugin:'license-report'
    apply plugin: 'java'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'maven'

    // Integration test plugin
    apply from: "$symAssembleDir/gradle/integrationTest.gradle"
    
    configurations { deployerJars }
    configurations { testArtifacts.extendsFrom testRuntime }

    configurations.provided.transitive = true
    configurations.optional.transitive = false

    sourceCompatibility=1.8
    targetCompatibility=1.8
    
    compileJava {
    	options.compilerArgs += '-Xlint:deprecation'
    }
    
    compileTestJava {
    	options.compilerArgs += '-Xlint:deprecation'
    }
    
    compileIntegrationTestJava {
        options.compilerArgs += '-Xlint:deprecation'
    }
    
    eclipse {   
        classpath {
            downloadSources = true
            downloadJavadoc = false
	        
	        file {
	        	whenMerged { classpath ->
			        def list = classpath.getEntries()
			        list.removeAll { it.kind=='lib' && it.path.contains('javax.validation') && it.path.contains('validation-api') }
			        classpath.setEntries(list)
			    }
	        }
        }
        
        jdt {
            sourceCompatibility = 1.8
            targetCompatibility = 1.8
        }
        
        project.file.whenMerged { project ->
            project.natures.remove('org.springsource.ide.eclipse.gradle.core.nature')
            project.natures.remove('org.eclipse.buildship.core.gradleprojectnature')            
        }
    }

    idea {
        module {
            testResourceDirs = []
            testSourceDirs = [ file('src/test/java'), file('src/test/groovy')]
            resourceDirs += file('src/test/resources')
            downloadJavadoc = true
            downloadSources = true
        }
    }   
    
    javadoc {
        failOnError = false
    }
    
    afterEvaluate {
        if(plugins.hasPlugin(JavaPlugin)) {
            
            // configuration here
            rootProject.tasks.generateJavadoc {
                source += files(sourceSets.collect { srcSet -> srcSet.allJava })
                classpath += files(sourceSets*.compileClasspath)
            }
    
        }
    }    
    
    install.enabled = !project.properties.containsKey('pro')
    uploadArchives.enabled = !project.properties.containsKey('pro')
    
    
    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    artifacts { archives sourcesJar }    
    
    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: deployUploadUrl) {
                authentication(userName: deployUser, password: deployPassword)
            }
        }
    }

    task deploy {
        description 'Shortcut for Maven users'
        dependsOn tasks.uploadArchives
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }
    
    test {
        ignoreFailures true
        systemProperty "test.server", System.getProperty("test.server", "h2")
        systemProperty "test.client", System.getProperty("test.client", "h2")
        systemProperty "test.root", System.getProperty("test.root", "h2")
        systemProperty "port.number", System.getProperty("port.number", "31415")
        systemProperty "db2i.db.user", System.getProperty("db2i.db.user")
        systemProperty "db2i.db.password", System.getProperty("db2i.db.password")
        minHeapSize = "128m"
        maxHeapSize = "512m"
        forkEvery=1
    }
    
    integrationTest {
        ignoreFailures true
        systemProperty "test.server", System.getProperty("test.server", "h2")
        systemProperty "test.client", System.getProperty("test.client", "h2")
        systemProperty "test.root", System.getProperty("test.root", "h2")
        systemProperty "port.number", System.getProperty("port.number", "31415")
        systemProperty "db2i.db.user", System.getProperty("db2i.db.user")
        systemProperty "db2i.db.password", System.getProperty("db2i.db.password")
        minHeapSize = "128m"
        maxHeapSize = "512m"
        forkEvery=1
    }
    
    jar {
        manifest {
            attributes("Implementation-Title": project.name,
                       "Implementation-Version": version,
                       "Project-Artifact": project.name,                       
                       "Build-Version": version,
                       "Build-Time": new SimpleDateFormat("yyyyMMdd-HHmm").format(new Date()),
                       "Build-Scm-Version": scmVersion)
        }    
    }

    artifacts { testArtifacts testJar }

    ext {
        bshVersion = '2.0b6'
        commonsBeanUtilsVersion = '1.9.4'
        commonsCliVersion = '1.4'
        commonsDbcpVersion = '1.4'
        commonsFileuploadVersion = '1.4'
        commonsIoVersion = '2.6'
        commonsLangVersion = '2.6'
        commonsCodecVersion = '1.14'
        commonsCollectionVersion = '3.2.2'
        commonsVfs = '1.0'
        hamcrestVersion = '1.3'
        h2Version = '1.3.176'
        derbyVersion = '10.14.2.0'
        postgresqlVersion =  '42.2.8'
        hsqldbVersion = '2.4.1'
        sqliteVersion = '3.25.2'
        javaMailVersion = '1.6.2'
        jdomVersion = '2.0.6'
        jaxbVersion = '2.3.1'
        jacksonVersion = '2.10.2'
        junitVersion = '4.13'
        log4jVersion = '1.2.17'
        slf4jVersion = '1.7.30'
        mockitoVersion = '3.2.4'
        powerMockVersion = '2.0.5'
        mysqlVersion = '5.1.45'
        servletVersion = '3.1.0'
        springVersion = '5.2.3.RELEASE'
        jtdsVersion = '1.3.1'
        bouncyCastleVersion = '1.64'
        animalSnifferVersion = '1.18'
        jnaVersion = '5.5.0'
        jettyVersion = '9.4.26.v20200117'
        websocketVersion = '1.1'
        env = System.getenv()
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.slf4j:jul-to-slf4j:$slf4jVersion"
        compile "org.slf4j:jcl-over-slf4j:$slf4jVersion"
        compile "org.slf4j:slf4j-log4j12:$slf4jVersion"
        compile "log4j:log4j:$log4jVersion"
        
        provided "mysql:mysql-connector-java:$mysqlVersion"
        provided "org.postgresql:postgresql:$postgresqlVersion"
        provided "jdbc:ojdbc8:18.3.0.0"
        provided "net.sourceforge.jtds:jtds:$jtdsVersion"
        provided "com.h2database:h2:$h2Version"

        compileOnly "org.apache.derby:derby:$derbyVersion"
        compileOnly "org.hsqldb:hsqldb:$hsqldbVersion"
        compileOnly "org.xerial:sqlite-jdbc:$sqliteVersion"
        compileOnly "jdbc.sybase:jconnect:7.7"

        compileOnly ("com.datastax.cassandra:cassandra-driver-core:3.1.4") {
            exclude group: 'org.slf4j'
            exclude group: 'com.google.guava'
        }
        compileOnly ("nl.cad:tps-parse:1.0.15-SNAPSHOT") {
        	exclude group: 'commons-lang', module: 'commons-lang'
        }
        compileOnly ("org.apache.kafka:kafka-clients:1.1.0") {
            exclude group: 'org.slf4j'
        }
        compileOnly ("org.apache.avro:avro:1.8.2") {
            exclude group: 'org.slf4j'
        }
		compileOnly ("io.confluent:kafka-avro-serializer:3.2.1") {
			exclude group: 'com.fasterxml.jackson.core'
            exclude group: 'org.slf4j'
        }        

        compileOnly "org.mongodb:mongo-java-driver:2.12.3"
        
        compileOnly ("com.amazon.redshift:redshift-jdbc42-no-awssdk:1.2.36.1060") {
			exclude group: 'org.apache.httpcomponents'
            exclude group: 'commons-logging'
            exclude group: 'com.fasterxml.jackson.core'
            exclude group: 'commons-codec'
        }
        
        compileOnly ("com.amazonaws:aws-java-sdk-s3:1.11.510") {
			exclude group: 'org.apache.httpcomponents'
            exclude group: 'commons-logging'
            exclude group: 'com.fasterxml.jackson.core'
            exclude group: 'commons-codec'
        }

        compileOnly ("com.microsoft.azure:azure-storage:8.1.0") {
            exclude group: 'com.fasterxml.jackson.core'
            exclude group: 'org.slf4j'
        }

        compileOnly ("net.snowflake:snowflake-jdbc:3.6.27") {
            exclude group: 'com.fasterxml.jackson.core'
        }
        
        compileOnly 'org.apache.phoenix:phoenix:5.0.0-HBase-2.0'

        compileOnly "org.apache.hbase:hbase-client:1.3.6"
        
        compileOnly ('com.google.cloud:google-cloud-bigquery:1.99.0') {
            exclude group: 'com.google.protobuf'
        }

        testCompile fileTree(dir: System.getProperty("user.home") + '/.symmetricds/lib', include: '*.jar')
        testCompile "junit:junit:$junitVersion"
        testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"
        testCompile "org.mockito:mockito-core:$mockitoVersion"
        testCompile "org.powermock:powermock-api-mockito2:$powerMockVersion"
        testCompile "org.powermock:powermock-module-junit4:$powerMockVersion"
        
        deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
        
    }    

    task develop {
        dependsOn tasks.cleanEclipse, tasks.eclipse, tasks.cleanIdea, tasks.idea
        
        doLast {
            def wstFacetXml = "$projectDir/.settings/org.eclipse.wst.common.project.facet.core.xml"
            println "Deleting $wstFacetXml"
            delete wstFacetXml 
        }
    }
    
}
